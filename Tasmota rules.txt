# Tasmota Rules

# Rules needed for wemos device to make motion sensors work

  rule1 on Switch1#State do publish2 stat/wemos/SWITCH1 %value% endon on Switch2#State do publish2 stat/wemos/SWITCH2 %value% endon

# Keypad Rules

rule1
  on System#Boot do backlog var1 7; var2 0 endon
  on Rules#Timer=1 do power2 0 endon 
  on Switch2#state=2 do backlog add1 1; scheme %var1%; Ruletimer1 10 endon 
  on Var1#state==12 do var1 7 endon 

rule2
  on Switch3#state=2 do backlog add2 1; color2 %var2%; Ruletimer1 10 endon
  on Var2#state==12 do var2=1 endon


# Pool pump Rules to power off when running empty after 1 minute usage
  rule1
    on System#Boot do Rule2 0 endon
    on Power#state=0 do Rule2 0 endon
    on Power#state=1 do Ruletimer1 60 endon
    on Rules#Timer=1 do Rule2 1 endon

  rule2
    on Energy#Power<450 do Power1 0 endon


########################## Water pump rules

# v1

rule1
  on System#Boot do BackLog publish cmnd/sonoff-water-valve/pulsetime 90; publish cmnd/sonoff-water-valve/power 1; TelePeriod 10 endon   
  on Tele-Energy#Power<600 do publish cmnd/sonoff-water-valve/power 1 endon   
  on Tele-Energy#Power>=600 do publish cmnd/sonoff-water-valve/power 0 endon

# v2 - open water valve to vacuum air off the system

rule1
  on System#Boot do BackLog Rule2 1; Ruletimer1 480; publish cmnd/sonoff-water-valve/pulsetime 90; publish cmnd/sonoff-water-valve/power 1; TelePeriod 10 endon
  on Power1#state=1 do Backlog Rule2 1; Ruletimer1 480 endon
  on Power1#state=0 do Rule2 0 endon
  on Rules#Timer=1 do Backlog Power 0; Ruletimer2 14400; Rule2 0 endon
  on Rules#timer=2 do Power 1 endon

rule2
  on Tele-Energy#Power<600 do publish cmnd/sonoff-water-valve/power 1 endon
  on Tele-Energy#Power>=600 do publish cmnd/sonoff-water-valve/power 0 endon

# v3 - power off after X seconds if power < 600w or after 8 minutes

rule1
  on System#Boot do BackLog Rule2 0; Ruletimer1 5; Ruletimer2 60; TelePeriod 10 endon
  on Power1#state=1 do BackLog Rule2 0; Ruletimer1 5; Ruletimer2 60 endon
  on Power1#state=0 do Ruletimer3 3600 endon
  on Rules#Timer=1 do Rule2 1 endon
  on Rules#Timer=2 do Power 0 endon
  on Rules#Timer=3 do Power 1 endon

rule2
  on Tele-Energy#Power>600 do Backlog Ruletimer2 480 endon
  
########################## Mobile heater rules

rule1
  on Tele-SI7021#Temperature<21 do Power 1 endon
  on Tele-SI7021#Temperature>21.5 do Power 0 endon

rule1
  on Tele-AM2301#Temperature<20 do Power 1 endon
  on Tele-AM2301#Temperature>20.5 do Power 0 endon

  ########################## Power management rules for devices always on

rule1 
  on System#Boot do BackLog Rule2 1 endon
  on Rules#Timer=1 do Backlog Rule2 1 endon

rule2
  on ENERGY#Voltage<205 do Backlog Power 0; Ruletimer1 120; Rule2 0 endon
  on ENERGY#Voltage>=208 do Power 1 endon